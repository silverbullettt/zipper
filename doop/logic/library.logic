#include "library/assignment-compatible.logic"
#include "library/checkcast.logic"
#include "library/class-initializers.logic"
#include "library/method-resolution.logic"
#include "library/string-constants.logic"
#include "library/subclass.logic"
#include "library/superinterface.logic"
#include "library/class-initialization.logic"

#ifdef EXCEPTIONS_PRECISE
#ifndef PADDLE_COMPAT
#include "library/exceptions/precise.logic"
#endif
#endif

#ifdef EXCEPTIONS_EXPERIMENTAL
#include "library/exceptions/experimental.logic"
#endif

#ifdef EXCEPTIONS_IMPRECISE
#include "library/exceptions/imprecise.logic"
#endif

#include "library/finalization.logic"
#include "library/implicit-reachable.logic"
#include "library/native.logic"
#include "library/reference.logic"

#ifndef PADDLE_COMPAT
#ifndef DISABLE_REFLECTION
#ifndef DISABLE_REFLECTIVE_METHS
#include "library/reflective.logic"
#endif
#include "library/reflection.logic" 
#endif
#endif

#include "library/threads.logic"

#ifdef CLIENT_EXCEPTION_FLOW
#include "client/exception-flow.logic"
#endif

#ifdef CLIENT_EXTENSIONS
#include "client/client-extensions-catalogue.logic"
#endif

/**
 * Declaring class
 */
// TODO declare this as a ClassType
DeclaringClassMethod[?method] = ?type ->
  Type(?type),
  MethodSignatureRef(?method).

DeclaringClassMethod[?method] = ?type <-
  MethodDeclaration[?signature] = ?method,
  MethodSignature:Type[?signature] = ?type.

// The Type is almost always a ClassType, except for Object methods
// dispatched on arrays. The InstructionRef is almost always a 
// HeapAllocationRef, except for reflectively allocated objects which
// have a regular invocation site as their id.
DeclaringClassAllocation[?heap] = ?type ->
  Type(?type),
  InstructionRef(?heap).

DeclaringClassAllocation[?heap] = ?type <-
  AssignHeapAllocation(?heap, _, ?inmethod),
  DeclaringClassMethod[?inmethod] = ?type,
  !StringConstant(?heap),
  !ClassConstant(?heap).

/*
DeclaringClassAllocation[?heap] = ?type <-
  ReflectiveHeapAllocation[_, _] = ?heap,
  HeapAllocation:Type[?heap] = ?type.
*/

DeclaringClassAllocation[?heap] = ?type <-
  ReflectiveHeapAllocation[_, ?type] = ?heap.

DeclaringClassAllocation[?heap] = ?type <-
  ReifiedConstructor[_] = ?heap,
  HeapAllocation:Type[?heap] = ?type.

DeclaringClassAllocation[?heap] = ?type <-
  ReifiedMethod[_] = ?heap,
  HeapAllocation:Type[?heap] = ?type.

DeclaringClassAllocation[?heap] = ?type <-
  ReifiedField[_] = ?heap,
  HeapAllocation:Type[?heap] = ?type.

DeclaringClassAllocation[?heap] = ?type <-
  ClassConstant(?heap),
  ClassTypeValue(?type, "java.lang.String").

/* REVIEW: the problem is that currently reflective allocations
   are not uniquely identified. This would produce way too many
   facts. So instead we cheat and use the type of the reflective
   allocation as the type *containing* the reflective allocation.
   If we are to fix this in the future, it will look like this:

DeclaringClassAllocation[?heap] = ?type <-
  ReflectiveHeapAllocation[?invocation, _] = ?heap,
  Instruction:Method[?invocation] = ?inmethod,
  DeclaringClassMethod[?inmethod] = ?type,
  !StringConstant(?heap).
*/

DeclaringClassAllocation[?heap] = ?type <-
  StringConstant(?heap),
  ClassTypeValue(?type, "java.lang.String").

// The following are special cases. Not clear where they belong.
DeclaringClassAllocation[?heap] = ?type <-
  HeapAllocationValue(?heap, "<<string-constant>>"),
  ClassTypeValue(?type, "java.lang.String").

DeclaringClassAllocation[?heap] = ?type <-
  HeapAllocationValue(?heap, "<<reflection-string-constant>>"),
  ClassTypeValue(?type, "java.lang.String").

DeclaringClassAllocation[?heap] = ?type <-
  HeapAllocationValue(?heap, "<<string-buffer>>"),
  ClassTypeValue(?type, "java.lang.String").

#if defined(JRE15) || defined(JRE16)
DeclaringClassAllocation[?heap] = ?type <-
  HeapAllocationValue(?heap, "<<string-builder>>"),
  ClassTypeValue(?type, "java.lang.String").
#endif

/* Helpful macro for debugging. Should be empty.
WhatAreWeMissing(?heap) -> HeapAllocationRef(?heap).
WhatAreWeMissing(?heap) <- 
  HeapAllocationRef(?heap),
  !exists DeclaringClassAllocation[?heap].
*/

// TODO declare this as a ClassType
Field:DeclaringClass[?signature] = ?type ->
  Type(?type),
  FieldSignatureRef(?signature).

Field:DeclaringClass[?signature] = ?declaringClass <-
  FieldSignature(?signature, ?declaringClass, _, _).

Field:Type[?signature] = ?type ->
  Type(?type),
  FieldSignatureRef(?signature).

Field:Type[?signature] = ?type <-
  FieldSignature(?signature, _, _, ?type).


/**
 * Joins of input facts, useful for performance reasons.
 */
VirtualMethodInvocation:SimpleName[?invocation] = ?simplename ->
  MethodInvocationRef(?invocation),
  SimpleNameRef(?simplename).

VirtualMethodInvocation:SimpleName[?invocation] = ?simplename <-
  VirtualMethodInvocation:Signature[?invocation] = ?signature,
  MethodSignature:SimpleName[?signature] = ?simplename.

VirtualMethodInvocation:Descriptor[?invocation] = ?descriptor ->
  MethodInvocationRef(?invocation),
  MethodDescriptorRef(?descriptor).

VirtualMethodInvocation:Descriptor[?invocation] = ?descriptor <-
  VirtualMethodInvocation:Signature[?invocation] = ?signature,
  MethodSignature:Descriptor[?signature] = ?descriptor.

Instruction:Method[?instruction] = ?inmethod ->
  InstructionRef(?instruction),
  MethodSignatureRef(?inmethod).

Instruction:Method[?invocation] = ?inmethod <-
  SpecialMethodInvocation:In(?invocation, ?inmethod).

Instruction:Method[?invocation] = ?inmethod <-
  VirtualMethodInvocation:In(?invocation, ?inmethod).

Instruction:Method[?invocation] = ?inmethod <-
  StaticMethodInvocation:In(?invocation, ?inmethod).

Instruction:Method[?invocation] = ?inmethod <-
  Throw:Method[?invocation] = ?inmethod.

/**
 * Main methods
 */
MainMethodDeclaration(?method) ->
  MethodSignatureRef(?method).

MainMethodDeclaration(?method) <-
  MainClass(?type),
  SimpleNameRef:Value(?simplename:"main"),
  MethodDescriptorRef:Value(?descriptor:"void(java.lang.String[])"),
  ModifierRef:Value(?public:"public"),
  ModifierRef:Value(?static:"static"),
  MethodSignature:Type[?signature] = ?type,
  MethodSignature:SimpleName[?signature] = ?simplename,
  MethodSignature:Descriptor[?signature] = ?descriptor,
  MethodDeclaration[?signature] = ?method,
  MethodModifier(?public, ?method),
  MethodModifier(?static, ?method).

